<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>Threejs Explorations 1710V-03</title>
  <link rel="stylesheet" href="https://public.codepenassets.com/css/normalize-5.0.0.min.css">
<link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<script id="fragmentShader" type="x-shader/x-fragment">
  precision highp float;
    uniform vec2 iResolution;
    uniform float iTime;
    uniform vec2 iMouse;
    uniform sampler2D iChannel0;
    uniform int shapeType;
    uniform int materialType;
    uniform float shapeSize;
    uniform vec4 shapeColor;
    uniform bool followMouse;
    uniform float armLength;
    uniform float armWidth;
    uniform float rotationSpeedX;
    uniform float rotationSpeedY;
    uniform float rotationSpeedZ;
    uniform float bevelSize;
    uniform float transparency;

    #define PI 3.14159265359

    mat3 rotateX(float theta) {
        float c = cos(theta);
        float s = sin(theta);
        return mat3(
            vec3(1, 0, 0),
            vec3(0, c, -s),
            vec3(0, s, c)
        );
    }

    mat3 rotateY(float theta) {
        float c = cos(theta);
        float s = sin(theta);
        return mat3(
            vec3(c, 0, s),
            vec3(0, 1, 0),
            vec3(-s, 0, c)
        );
    }

    mat3 rotateZ(float theta) {
        float c = cos(theta);
        float s = sin(theta);
        return mat3(
            vec3(c, -s, 0),
            vec3(s, c, 0),
            vec3(0, 0, 1)
        );
    }

    float sdBeveledBox(vec3 p, vec3 b, float bevel) {
        vec3 q = abs(p) - b + bevel;
        return min(max(q.x, max(q.y, q.z)), 0.0) + length(max(q, 0.0)) - bevel;
    }

    float sdCross(vec3 p) {
        float bx = sdBeveledBox(p.xyz, vec3(armLength, armWidth, armWidth), bevelSize);
        float by = sdBeveledBox(p.yzx, vec3(armWidth, armLength, armWidth), bevelSize);
        float bz = sdBeveledBox(p.zxy, vec3(armWidth, armWidth, armLength), bevelSize);
        return min(bx, min(by, bz));
    }

    float sdSphere(vec3 p, float r) {
        return length(p) - r;
    }

    float sdTorus(vec3 p, vec2 t) {
        vec2 q = vec2(length(p.xz) - t.x, p.y);
        return length(q) - t.y;
    }

    float sdOctahedron(vec3 p, float s) {
        p = abs(p);
        return (p.x + p.y + p.z - s) * 0.57735027;
    }

    float sdCapsule(vec3 p, vec3 a, vec3 b, float r) {
        vec3 pa = p - a, ba = b - a;
        float h = clamp(dot(pa, ba) / dot(ba, ba), 0.0, 1.0);
        return length(pa - ba * h) - r;
    }

    float sdMandelbulb(vec3 p) {
        vec3 w = p;
        float m = dot(w, w);
        vec4 trap = vec4(abs(w), m);
        float dz = 1.0;
        for(int i = 0; i < 4; i++) {
            float m2 = m * m;
            float m4 = m2 * m2;
            dz = 8.0 * sqrt(m4 * m2 * m) * dz + 1.0;
            float x = w.x; float x2 = x * x; float x4 = x2 * x2;
            float y = w.y; float y2 = y * y; float y4 = y2 * y2;
            float z = w.z; float z2 = z * z; float z4 = z2 * z2;
            float k3 = x2 + z2;
            float k2 = inversesqrt(k3 * k3 * k3 * k3 * k3 * k3 * k3);
            float k1 = x4 + y4 + z4 - 6.0 * y2 * z2 - 6.0 * x2 * y2 + 2.0 * z2 * x2;
            float k4 = x2 - y2 + z2;
            w.x = p.x + 64.0 * x * y * z * (x2 - z2) * k4 * (x4 - 6.0 * x2 * z2 + z4) * k1 * k2;
            w.y = p.y + -16.0 * y2 * k3 * k4 * k4 + k1 * k1;
            w.z = p.z + -8.0 * y * k4 * (x4 * x4 - 28.0 * x4 * x2 * z2 + 70.0 * x4 * z4 - 28.0 * x2 * z2 * z4 + z4 * z4) * k1 * k2;
            trap = min(trap, vec4(abs(w), m));
            m = dot(w, w);
            if(m > 256.0) break;
        }
        return 0.25 * log(m) * sqrt(m) / dz;
    }

    float sdShape(vec3 p) {
        p /= shapeSize;
        float d = 1e10;
        if (shapeType == 0) d = sdCross(p);
        else if (shapeType == 1) d = sdSphere(p, 0.5);
        else if (shapeType == 2) d = sdTorus(p, vec2(0.4, 0.1));
        else if (shapeType == 3) d = sdOctahedron(p, 0.5);
        else if (shapeType == 4) d = sdCapsule(p, vec3(0.0, -0.3, 0.0), vec3(0.0, 0.3, 0.0), 0.1);
        else if (shapeType == 5) d = sdMandelbulb(p);
        return d * shapeSize;
    }

    float rayMarch(vec3 ro, vec3 rd, mat3 rotation) {
        float tot = 0.0;
        for (int i = 0; i < 100; i++) {
            vec3 p = ro + tot * rd;
            float d = sdShape(rotation * p);
            tot += d;
            if (d < 0.001 || tot > 20.0) break;
        }
        return tot;
    }

    float fresnel(vec3 normal, vec3 viewDir, float power) {
        return pow(1.0 - abs(dot(normal, viewDir)), power);
    }

    void mainImage(out vec4 fragColor, in vec2 fragCoord) {
        vec2 uv = fragCoord / iResolution.xy;
        vec2 p = (fragCoord - 0.5 * iResolution.xy) / iResolution.y;
        
        vec2 targetPos = vec2(0.0);
        if (followMouse) {
            targetPos = (iMouse.xy / iResolution.xy) * 2.0 - 1.0;
            targetPos.x *= iResolution.x / iResolution.y;
        }
        
        vec2 currentPos = vec2(0.0);
        if (followMouse && iMouse.x != 0. && iMouse.y != 0.) {
          currentPos += (targetPos - currentPos) * 1.;
        }
        
        vec3 ro = vec3(currentPos, 2.0);
        vec3 rd = normalize(vec3(p - currentPos, -1.0));
        
        mat3 rotX = rotateX(iTime * rotationSpeedX);
        mat3 rotY = rotateY(iTime * rotationSpeedY);
        mat3 rotZ = rotateZ(iTime * rotationSpeedZ);
        mat3 rotation = rotZ * rotY * rotX;
        
        float d = rayMarch(ro, rd, rotation);
        
        vec3 bgColor = texture(iChannel0, uv).rgb;
        
        vec3 col = bgColor;
        float alpha = 1.0;
        if (d < 20.0) {
            vec3 p = ro + d * rd;
            vec3 normal = normalize(vec3(
                sdShape(rotation * (p + vec3(0.001, 0.0, 0.0))) - sdShape(rotation * (p - vec3(0.001, 0.0, 0.0))),
                sdShape(rotation * (p + vec3(0.0, 0.001, 0.0))) - sdShape(rotation * (p - vec3(0.0, 0.001, 0.0))),
                sdShape(rotation * (p + vec3(0.0, 0.0, 0.001))) - sdShape(rotation * (p - vec3(0.0, 0.0, 0.001)))
            ));
            normal = rotation * normal;
            vec3 lightDir = normalize(vec3(1.0, 1.0, 1.0));
            float diff = max(dot(normal, lightDir), 0.0);
            
            vec3 objectColor;
            if (materialType == 0) {
                float spec = pow(max(dot(reflect(-lightDir, normal), -rd), 0.0), 32.0);
                float fresnelFactor = fresnel(normal, -rd, 5.0);
                objectColor = shapeColor.rgb * (diff * 0.5 + 0.5) + vec3(1.0) * spec + fresnelFactor * 0.5;
            } else if (materialType == 1) {
                float spec = pow(max(dot(reflect(-lightDir, normal), -rd), 0.0), 16.0);
                objectColor = shapeColor.rgb * diff * 0.8 + vec3(0.2) + vec3(1.0) * spec * 0.5;
            } else if (materialType == 2) {
                float fresnelFactor = fresnel(normal, -rd, 3.0);
                vec3 refractDir = refract(rd, normal, 1.0 / 1.5);
                vec3 reflectDir = reflect(rd, normal);
                vec3 refractCol = texture(iChannel0, (p.xy + refractDir.xy) * 0.5 + 0.5).rgb;
                vec3 reflectCol = texture(iChannel0, (p.xy + reflectDir.xy) * 0.5 + 0.5).rgb;
                objectColor = mix(refractCol, reflectCol, fresnelFactor) + shapeColor.rgb * 0.2;
            }
            
            col = mix(bgColor, objectColor, transparency);
            alpha = transparency;
        }
        
        fragColor = vec4(col, alpha);
    }

    void main() {
        vec2 fragCoord = gl_FragCoord.xy;
        vec4 fragColor;
        mainImage(fragColor, fragCoord);
        gl_FragColor = fragColor;
    }
    </script>
<script id="vertexShader" type="x-shader/x-vertex">
  void main() {
        gl_Position = vec4(position, 1.0);
    }
    </script>

<div class="container">
  <div class="sidebar">
    <div class="logo">NX<sup>Â°</sup></div>
    <p class="description">THE ARCHIVE COLLECTS RECORDS OF ABANDONED WORLDS AND LOST TECHNOLOGIES, WAITING TO BE DISCOVERED.</p>
    <nav class="nav-links">
      <a href="#">_DATA VAULTS</a>
      <a href="#">_DEEP SPACE</a>
      <a href="#">_FORBIDDEN ZONES</a>
      <a href="#">_EXODUS LOGS</a>
    </nav>
    <div class="footer">
      <p>Err: [404 - SIGNAL LOST]<br />
        SYSTEM TIME: CYCLE 2187.42</p>
    </div>
    <p class="division">
      ACCESS LEVEL: RESTRICTED.<br>
      TRACE INITIATED: SOURCE UNKNOWN.</p>
  </div>
  <div class="main-content">
    <p class="signal">_Uplink Pending...</p>
  </div>
</div>
<!-- partial -->
  <script src='https://cdn.jsdelivr.net/npm/gsap@3.12.5/dist/gsap.min.js'></script>
<script src='https://cdn.jsdelivr.net/npm/gsap@3.12.5/dist/ScrollTrigger.min.js'></script>
<script src='https://cdn.skypack.dev/dat.gui'></script><script type="module" src="./script.js"></script>

</body>
</html>
